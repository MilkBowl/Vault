apply plugin: 'maven-publish'

ext.isRelease = {
  return !project.version.endsWith("-SNAPSHOT")
}

publishing {
  repositories {
    if (project.hasProperty("deployUrl")) {
      maven {
        credentials {
          username = "${deployUser}"
          password = "${deployPass}"
        }
        url = String.format("${deployUrl}", isRelease ? 'releases' : 'snapshots')
      }
    }
  }

  publications {
    maven(MavenPublication) {
      from components.java

      artifact sourceJar
      artifact javadocJar

      pom.withXml {
        asNode().children().last() + {
          resolveStrategy = DELEGATE_FIRST

          name 'Vault'
          description project.description
          url 'https://natrolite.org/'

          ciManagement {
            system 'Travis CI'
            url 'https://travis-ci.org/natrolite/vault'
          }

          issueManagement {
            system 'GitHub Issues'
            url 'https://github.com/natrolite/vault/issues'
          }

          licenses {
            license {
              name 'GNU Lesser General Public License (LGPL)'
              url 'https://www.gnu.org/licenses/lgpl.txt'
              distribution 'repo'
            }
          }

          scm {
            url 'https://github.com/natrolite/vault'
            connection 'scm:git:git://github.com/natrolite/vault.git'
            developerConnection 'scm:git:ssh://github.com:natrolite/vault.git'
          }
        }
      }
    }
  }
}
